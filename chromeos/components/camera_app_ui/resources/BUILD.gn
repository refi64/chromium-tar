# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromeos/components/camera_app_ui/camera_app_ui.gni")

group("closure_compile") {
  deps = [ "js:closure_compile" ]
}

group("chrome_camera_app") {
  # According to crbug.com/855747, we should list all the files we want to copy
  # rather than list only the folders to avoid potential building issue and ease
  # the difficulty to diagnose.
  deps = [ "strings" ]

  data_deps = [
    ":chrome_camera_app_base",
    ":chrome_camera_app_css",
    ":chrome_camera_app_images",
    ":chrome_camera_app_js",
    ":chrome_camera_app_js_browser_proxy",
    ":chrome_camera_app_js_device",
    ":chrome_camera_app_js_lib",
    ":chrome_camera_app_js_models",
    ":chrome_camera_app_js_mojo",
    ":chrome_camera_app_js_views",
    ":chrome_camera_app_js_views_camera",
    ":chrome_camera_app_js_views_camera_mode",
    ":chrome_camera_app_js_window_controller",
    ":chrome_camera_app_mojo_generated",
    ":chrome_camera_app_preload_images_js",
    ":chrome_camera_app_sounds",
    ":chrome_camera_app_views",
  ]
}

copy("chrome_camera_app_base") {
  sources = [ "manifest.json" ]

  outputs = [ "$out_camera_app_dir/{{source_file_part}}" ]
}

copy("chrome_camera_app_css") {
  sources = [ "css/main.css" ]

  outputs = [ "$out_camera_app_dir/css/{{source_file_part}}" ]
}

import("images/images.gni")

copy("chrome_camera_app_images") {
  sources = [
    "images/camera_app_icons_128.png",
    "images/camera_app_icons_48.png",
  ]
  foreach(asset, in_app_assets) {
    sources += [ "images/$asset" ]
  }

  outputs = [ "$out_camera_app_dir/images/{{source_file_part}}" ]
}

copy("chrome_camera_app_preload_images_js") {
  deps = [ "js:gen_preload_images_js" ]
  js_gen_dir = get_label_info(deps[0], "target_gen_dir")
  sources = [ "$js_gen_dir/preload_images.js" ]

  outputs = [ "$out_camera_app_dir/js/{{source_file_part}}" ]
}

copy("chrome_camera_app_js") {
  sources = [
    "js/app_window.js",
    "js/async_job_queue.js",
    "js/background.js",
    "js/background_ops.js",
    "js/barcode_chip.js",
    "js/chrome_util.js",
    "js/dom.js",
    "js/dynamic_import.js",
    "js/error.js",
    "js/gallerybutton.js",
    "js/init.js",
    "js/intent.js",
    "js/main.js",
    "js/metrics.js",
    "js/nav.js",
    "js/perf.js",
    "js/snackbar.js",
    "js/sound.js",
    "js/state.js",
    "js/test_bridge.js",
    "js/timer.js",
    "js/toast.js",
    "js/tooltip.js",
    "js/type.js",
    "js/untrusted_ga_helper.js",
    "js/untrusted_helper_interfaces.js",
    "js/untrusted_script_loader.js",
    "js/untrusted_video_processor_helper.js",
    "js/util.js",
    "js/waitable_event.js",
  ]

  outputs = [ "$out_camera_app_dir/js/{{source_file_part}}" ]
}

copy("chrome_camera_app_js_browser_proxy") {
  sources = [
    # TODO(b/129956426): Remove dependency used only in closure compiler check.
    "js/browser_proxy/browser_proxy.js",
    "js/browser_proxy/browser_proxy_interface.js",
  ]

  outputs = [ "$out_camera_app_dir/js/browser_proxy/{{source_file_part}}" ]
}

copy("chrome_camera_app_js_device") {
  sources = [
    "js/device/camera3_device_info.js",
    "js/device/constraints_preferrer.js",
    "js/device/device_info_updater.js",
  ]

  outputs = [ "$out_camera_app_dir/js/device/{{source_file_part}}" ]
}

copy("chrome_camera_app_js_lib") {
  sources = [
    "js/lib/analytics.js",
    "js/lib/comlink.js",
    "js/lib/ffmpeg.js",
    "js/lib/ffmpeg.wasm",
  ]

  outputs = [ "$out_camera_app_dir/js/lib/{{source_file_part}}" ]
}

copy("chrome_camera_app_js_models") {
  sources = [
    "js/models/async_interval.js",
    "js/models/async_writer.js",
    "js/models/barcode.js",
    "js/models/barcode_worker.js",
    "js/models/barcode_worker_interface.js",
    "js/models/chrome_file_system_entry.js",
    "js/models/file_namer.js",
    "js/models/file_system.js",
    "js/models/file_system_entry.js",
    "js/models/file_util.js",
    "js/models/lazy_directory_entry.js",
    "js/models/mp4_video_processor.js",
    "js/models/result_saver.js",
    "js/models/video_processor_interface.js",
    "js/models/video_saver.js",
  ]

  outputs = [ "$out_camera_app_dir/js/models/{{source_file_part}}" ]
}

copy("chrome_camera_app_js_mojo") {
  sources = [
    "js/mojo/chrome_helper.js",
    "js/mojo/device_operator.js",
    "js/mojo/image_capture.js",
  ]

  outputs = [ "$out_camera_app_dir/js/mojo/{{source_file_part}}" ]
}

copy("chrome_camera_app_js_views") {
  sources = [
    "js/views/camera.js",
    "js/views/camera_intent.js",
    "js/views/dialog.js",
    "js/views/settings.js",
    "js/views/view.js",
    "js/views/warning.js",
  ]

  outputs = [ "$out_camera_app_dir/js/views/{{source_file_part}}" ]
}

copy("chrome_camera_app_js_views_camera") {
  sources = [
    "js/views/camera/layout.js",
    "js/views/camera/options.js",
    "js/views/camera/preview.js",
    "js/views/camera/review_result.js",
    "js/views/camera/timertick.js",
  ]

  outputs = [ "$out_camera_app_dir/js/views/camera/{{source_file_part}}" ]
}

copy("chrome_camera_app_js_views_camera_mode") {
  sources = [
    "js/views/camera/mode/index.js",
    "js/views/camera/mode/mode_base.js",
    "js/views/camera/mode/photo.js",
    "js/views/camera/mode/portrait.js",
    "js/views/camera/mode/record_time.js",
    "js/views/camera/mode/square.js",
    "js/views/camera/mode/video.js",
  ]

  outputs = [ "$out_camera_app_dir/js/views/camera/mode/{{source_file_part}}" ]
}

copy("chrome_camera_app_js_window_controller") {
  sources = [
    "js/window_controller/mojo_window_controller.js",
    "js/window_controller/window_controller.js",
    "js/window_controller/window_controller_interface.js",
  ]

  outputs = [ "$out_camera_app_dir/js/window_controller/{{source_file_part}}" ]
}

copy("chrome_camera_app_sounds") {
  sources = [
    "sounds/record_end.ogg",
    "sounds/record_pause.ogg",
    "sounds/record_start.ogg",
    "sounds/shutter.ogg",
    "sounds/tick_final.ogg",
    "sounds/tick_inc.ogg",
    "sounds/tick_start.ogg",
  ]

  outputs = [ "$out_camera_app_dir/sounds/{{source_file_part}}" ]
}

copy("chrome_camera_app_views") {
  sources = [
    "views/background.html",
    "views/main.html",
    "views/untrusted_script_loader.html",
  ]

  outputs = [ "$out_camera_app_dir/views/{{source_file_part}}" ]
}

copy("chrome_camera_app_mojo_generated") {
  sources = [
    "$root_gen_dir/chromeos/components/camera_app_ui/camera_app_helper.mojom-lite.js",
    "$root_gen_dir/components/arc/mojom/camera_intent.mojom-lite.js",
    "$root_gen_dir/media/capture/mojom/image_capture.mojom-lite.js",
    "$root_gen_dir/media/capture/video/chromeos/mojom/camera_app.mojom-lite.js",
    "$root_gen_dir/media/capture/video/chromeos/mojom/camera_common.mojom-lite.js",
    "$root_gen_dir/media/capture/video/chromeos/mojom/camera_metadata.mojom-lite.js",
    "$root_gen_dir/media/capture/video/chromeos/mojom/camera_metadata_tags.mojom-lite.js",
    "$root_gen_dir/mojo/public/js/mojo_bindings_lite.js",
    "$root_gen_dir/mojo/public/mojom/base/time.mojom-lite.js",
    "$root_gen_dir/third_party/blink/public/mojom/idle/idle_manager.mojom-lite.js",
    "$root_gen_dir/ui/gfx/geometry/mojom/geometry.mojom-lite.js",
    "$root_gen_dir/ui/gfx/range/mojom/range.mojom-lite.js",
  ]

  deps = [
    "//chromeos/components/camera_app_ui:mojo_bindings_js",
    "//media/capture/video/chromeos/mojom:cros_camera_js",
    "//mojo/public/js:bindings_lite",
    "//third_party/blink/public/mojom:mojom_platform_js",
  ]

  outputs = [ "$out_camera_app_dir/js/mojo/{{source_file_part}}" ]
}
