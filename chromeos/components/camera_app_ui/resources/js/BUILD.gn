# Copyright 2019 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//chromeos/components/camera_app_ui/resources/images/images.gni")
import("//third_party/closure_compiler/compile_js.gni")

js_type_check("closure_compile") {
  closure_flags =
      default_closure_args + [
        "language_in=ECMASCRIPT_2018",
        "jscomp_error=lintChecks",
        "jscomp_error=strictCheckTypes",
        "conformance_configs=" +
            rebase_path("externs/conformance_config.textproto", root_build_dir),
        "jscomp_error=conformanceViolations",
        "hide_warnings_for=mojo/public/js/",
        "hide_warnings_for=gen/",
        "hide_warnings_for=js/lib/",

        # For all js module in source tree.
        "js_module_root=" + rebase_path(".", root_build_dir),

        # For all dynamic generated js module.
        "js_module_root=" + rebase_path(target_gen_dir, root_build_dir),
      ]
  deps = [ ":compile_resources" ]
}

action("gen_preload_images_js") {
  script = "../utils/gen_preload_images_js.py"
  inputs = []
  foreach(asset, in_app_assets) {
    inputs += [ "../images/$asset" ]
  }

  outputs = [ "$target_gen_dir/preload_images.js" ]
  response_file_contents = inputs
  args = [
    "--output_file",
    rebase_path(outputs[0], root_build_dir),
    "--images_list_file={{response_file_name}}",
  ]
}

js_library("preload_images") {
  extra_deps = [ ":gen_preload_images_js" ]
  sources = [ "$target_gen_dir/preload_images.js" ]
  deps = []
}

js_library("compile_resources") {
  sources = [
    "app_window.js",
    "async_job_queue.js",
    "background.js",
    "background_ops.js",
    "barcode_chip.js",
    "browser_proxy/browser_proxy.js",
    "browser_proxy/browser_proxy_interface.js",
    "browser_proxy/webui_browser_proxy.js",
    "chrome_util.js",
    "device/camera3_device_info.js",
    "device/constraints_preferrer.js",
    "device/device_info_updater.js",
    "dom.js",
    "error.js",
    "gallerybutton.js",
    "init.js",
    "intent.js",
    "lib/comlink.js",
    "lib/ffmpeg.js",
    "main.js",
    "metrics.js",
    "models/async_interval.js",
    "models/async_writer.js",
    "models/barcode.js",
    "models/barcode_worker.js",
    "models/barcode_worker_interface.js",
    "models/chrome_file_system_entry.js",
    "models/file_namer.js",
    "models/file_system.js",
    "models/file_system_entry.js",
    "models/file_util.js",
    "models/idb.js",
    "models/lazy_directory_entry.js",
    "models/mp4_video_processor.js",
    "models/native_file_system_entry.js",
    "models/result_saver.js",
    "models/video_processor_interface.js",
    "models/video_saver.js",
    "mojo/chrome_helper.js",
    "mojo/device_operator.js",
    "mojo/image_capture.js",
    "nav.js",
    "perf.js",
    "snackbar.js",
    "sound.js",
    "state.js",
    "test_bridge.js",
    "timer.js",
    "toast.js",
    "tooltip.js",
    "type.js",
    "untrusted_ga_helper.js",
    "untrusted_helper_interfaces.js",
    "untrusted_script_loader.js",
    "untrusted_video_processor_helper.js",
    "util.js",
    "views/camera.js",
    "views/camera/layout.js",
    "views/camera/mode/index.js",
    "views/camera/mode/mode_base.js",
    "views/camera/mode/photo.js",
    "views/camera/mode/portrait.js",
    "views/camera/mode/record_time.js",
    "views/camera/mode/square.js",
    "views/camera/mode/video.js",
    "views/camera/options.js",
    "views/camera/preview.js",
    "views/camera/review_result.js",
    "views/camera/timertick.js",
    "views/camera_intent.js",
    "views/dialog.js",
    "views/settings.js",
    "views/view.js",
    "views/warning.js",
    "waitable_event.js",
    "window_controller/mojo_window_controller.js",
    "window_controller/window_controller.js",
    "window_controller/window_controller_interface.js",
  ]
  deps = [
    ":preload_images",
    "//chromeos/components/camera_app_ui:mojo_bindings_js_library_for_compile",
    "//components/arc/mojom:camera_intent_js_library_for_compile",
    "//media/capture/mojom:image_capture_js_library_for_compile",
    "//media/capture/video/chromeos/mojom:cros_camera_js_library_for_compile",
    "//third_party/blink/public/mojom:mojom_platform_js_library_for_compile",
  ]
  externs_list = [
    "externs/chrome.js",
    "externs/swa.js",
    "externs/universal_analytics_api.js",
    "externs/w3c_api.js",
    "$externs_path/chrome_extensions.js",
    "$externs_path/file_manager_private.js",
    "$externs_path/file_system_provider.js",
    "$externs_path/metrics_private.js",

    # TODO(crbug.com/980846): Remove it once it is upstreamed to the closure
    # compiler.
    "//chromeos/components/web_applications/externs/file_handling.externs.js",
  ]
}
