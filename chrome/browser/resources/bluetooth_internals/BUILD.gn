# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/grit_rule.gni")
import("//ui/webui/resources/tools/generate_grd.gni")

js_type_check("closure_compile") {
  deps = [ ":bluetooth_internals" ]
}

js_library("bluetooth_internals") {
  sources = [
    "adapter_broker.js",
    "adapter_page.js",
    "bluetooth_internals.js",
    "characteristic_list.js",
    "debug_log_page.js",
    "descriptor_list.js",
    "device_broker.js",
    "device_collection.js",
    "device_details_page.js",
    "device_table.js",
    "devices_page.js",
    "expandable_list.js",
    "object_fieldset.js",
    "service_list.js",
    "sidebar.js",
    "snackbar.js",
    "value_control.js",
  ]

  deps = [
    ":node_utils",
    ":page",
    ":page_manager",
    "//chrome/browser/ui/webui/bluetooth_internals:mojo_bindings_js_library_for_compile",
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js:util",
    "//ui/webui/resources/js/cr/ui:array_data_model",
    "//ui/webui/resources/js/cr/ui:list",
    "//ui/webui/resources/js/cr/ui:list_item",
  ]
}

js_library("node_utils") {
  deps = [ "//ui/webui/resources/js:cr" ]
}

js_library("page") {
  deps = [
    ":node_utils",
    "//ui/webui/resources/js:cr",
    "//ui/webui/resources/js:util",
    "//ui/webui/resources/js/cr:event_target",
    "//ui/webui/resources/js/cr/ui:bubble",
    "//ui/webui/resources/js/cr/ui:focus_outline_manager",
    "//ui/webui/resources/js/cr/ui:overlay",
  ]
}

js_library("page_manager") {
  deps = [
    ":page",
    "//ui/webui/resources/js:cr",
  ]
}

bluetooth_grd_prefix = "bluetooth_internals"
public_mojo_grdp_file = "$target_gen_dir/public_mojo_resources.grdp"
internals_mojo_grdp_file = "$target_gen_dir/internals_mojo_resources.grdp"
resources_grd_file = "$target_gen_dir/resources.grd"

generate_grd("build_public_mojo_grdp") {
  grd_prefix = bluetooth_grd_prefix
  out_grd = public_mojo_grdp_file
  input_files = [
    "adapter.mojom-lite.js",
    "uuid.mojom-lite.js",
    "device.mojom-lite.js",
  ]
  input_files_base_dir =
      rebase_path("$root_gen_dir/device/bluetooth/public/mojom/",
                  root_build_dir)
}

generate_grd("build_internal_mojo_grdp") {
  grd_prefix = bluetooth_grd_prefix
  out_grd = internals_mojo_grdp_file
  input_files = [ "bluetooth_internals.mojom-lite.js" ]
  input_files_base_dir =
      rebase_path("$root_gen_dir/chrome/browser/ui/webui/bluetooth_internals/",
                  root_build_dir)
}

generate_grd("build_grd") {
  grd_prefix = bluetooth_grd_prefix
  out_grd = resources_grd_file
  deps = [
    ":build_internal_mojo_grdp",
    ":build_public_mojo_grdp",
  ]
  input_files = [
    "adapter_broker.js",
    "adapter_page.js",
    "debug_log_page.js",
    "characteristic_list.js",
    "bluetooth_internals.css",
    "descriptor_list.js",
    "device_broker.js",
    "device_collection.js",
    "device_details_page.js",
    "device_table.js",
    "devices_page.js",
    "expandable_list.js",
    "bluetooth_internals.html",
    "bluetooth_internals.js",
    "menu.svg",
    "node_utils.js",
    "object_fieldset.js",
    "page_manager.js",
    "page.js",
    "service_list.js",
    "sidebar.js",
    "snackbar.js",
    "value_control.js",
  ]
  input_files_base_dir = rebase_path("./", "//")

  grdp_files = [
    public_mojo_grdp_file,
    internals_mojo_grdp_file,
  ]
}

grit("resources") {
  enable_input_discovery_for_gn_analyze = false
  source = resources_grd_file

  deps = [
    ":build_grd",
    "//chrome/browser/ui/webui/bluetooth_internals:mojo_bindings_js",
    "//device/bluetooth/public/mojom:deprecated_experimental_interfaces_js",
    "//device/bluetooth/public/mojom:mojom_js",
  ]

  outputs = [
    "grit/bluetooth_internals_resources.h",
    "grit/bluetooth_internals_resources_map.h",
    "grit/bluetooth_internals_resources_map.cc",
    "bluetooth_internals_resources.pak",
  ]
  output_dir = "$root_gen_dir/chrome"
}
